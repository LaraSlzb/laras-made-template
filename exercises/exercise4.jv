pipeline TemperaturePipeline {

  TemperatureExtractor
    -> ZipArchiveInterpreter
    -> DataFilePicker
    -> TemperatureTextFileInterpreter
    -> TemperatureCSVInterpreter
    -> HeaderSequenceWriter
    -> TemperatureTableInterpreter
    -> TemperatureTransformer
    -> BatterieTempTransformer
    ->TemperatureLoader;


  block TemperatureExtractor oftype HttpExtractor {
    url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
  }

 block ZipArchiveInterpreter oftype ArchiveInterpreter {
   archiveType: "zip";
 }
  block DataFilePicker oftype FilePicker {
   path: "./data.csv";
 }

  block TemperatureTextFileInterpreter oftype TextFileInterpreter { }

  block TemperatureCSVInterpreter oftype CSVInterpreter {
    delimiter: ';';
  }

 block HeaderSequenceWriter oftype CellWriter {
   at: range A1:J1;
   write: ['id', 'producer', 'model', 'month', 'temperature', 'a', 'b', 'c', 'd', 'battery_temperature'];
 }

  block TemperatureTableInterpreter oftype TableInterpreter {
    header: true;
    columns: [
        "id" oftype integer,
        "producer" oftype text,
        "model" oftype text,
        "month" oftype monat,
        "temperature" oftype decimal,
        "battery_temperature" oftype decimal,
    ];
  }
  


valuetype geraet oftype integer {
    constraints:[
        OverZero,
    ];
}
constraint OverZero on integer: value > 0;
constraint Less13 on integer: value < 13;


valuetype monat oftype integer {
    constraints:[
        OverZero,
        Less13
    ];
}

block TemperatureTransformer oftype TableTransformer {
    inputColumns: [
      "temperature"
    ];
    outputColumn: "temperature";
    uses: CelsiusToFahrenheit;
  }
 block BatterieTempTransformer oftype TableTransformer {
    inputColumns: [
      "battery_temperature"
    ];
    outputColumn: "battery_temperature";
    uses: CelsiusToFahrenheit;
  }

  transform CelsiusToFahrenheit {
    from celsius oftype decimal;
    to fahrenheit oftype decimal;

    fahrenheit: (celsius * 9/5) + 32;
  }


  block TemperatureLoader oftype SQLiteLoader {
    table: "temperatures";
    file: "./temperatures.sqlite";
  }
}